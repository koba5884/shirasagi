class @Gws_Schedule_Calendar
  @render: (selector, opts = {})->
    params = @defaultParams(opts)
    $.extend params, @editableParams(opts) if opts['restUrl']
    $.extend params, @tapMenuParams(opts) if opts['restUrl']
    $.extend params, opts
    $(selector).fullCalendar(params)

    $(document).click (ev) ->
      $(".fc-event").not($(ev.target).closest(".fc-event")).find(".fc-popup").remove()

  @defaultParams: (opts) ->
    lang: 'ja'
    titleFormat: {
      month: "<%= I18n.t('gws/schedule.calendar.titleFormat.month') %>",
      week: "<%= I18n.t('gws/schedule.calendar.titleFormat.week') %>",
      day: "<%= I18n.t('gws/schedule.calendar.titleFormat.day') %>"
    },
    timeFormat: 'HH:mm'
    slotLabelFormat: 'HH:mm',
    columnFormat: { month: "ddd", week: "M/D [(]ddd[)]", day: "M/D [(]ddd[)]" }
    header: { left: 'today prev next title', right: 'month basicWeek agendaDay' },
    contentHeight: 'auto'
    fixedWeekCount: false
    startParam: 's[start]'
    endParam: 's[end]'
    loading: (isLoading, view) ->
      if isLoading
        $(this).find('.fc-day:first-child').append('<span class="fc-loading">Loading..</span>')
      else
        $(this).find('.fc-loading').remove()

  @tapMenuParams: (opts) ->
    url = opts['restUrl']

    dayClick: (date, event, view) ->
      links  = "<a href='#{url}/new?start=#{date.format()}' class='add-plan'><%= I18n.t 'gws/schedule.links.add_plan' %></a>"
      if opts['defaultView'] != 'basicWeek'
        if view.name != 'month'
          links += '<a href="" data-view="month"><%= I18n.t "gws/schedule.links.show_month" %></a>'
        if view.name != 'basicWeek'
          links += '<a href="" data-view="basicWeek"><%= I18n.t "gws/schedule.links.show_week" %></a>'
        if view.name != 'agendaDay'
          links += '<a href="" data-view="agendaDay"><%= I18n.t "gws/schedule.links.show_day" %></a>'
      $("body").append('<div class="tap-menu">' + links + '</div>')
      $(".tap-menu").show().css("left", event.pageX - 5).css("top", event.pageY - 5).show()

      $(".tap-menu a").click ->
        if $(this).data('view')
          cal = view.calendar.getCalendar()
          cal.changeView($(this).data('view'));
          cal.gotoDate(date);
          $(".tap-menu").remove()
          return false
      $(".tap-menu").mouseleave ->
        $(".tap-menu").remove()

  @editableParams: (opts)->
    url = opts['restUrl']

    editable: true,
    eventClick: (event, jsEvent, view) ->
      return if $(this).hasClass('fc-holiday')
      return if $(this).find('.gws-schedule-popup').length
      return unless event.readable

      h = "<div class='fc-popup'>"
      h += "<div class='title'><a href='#{url}/#{event.id}'>#{event.title}</a></div>"
      h += "<div class='description'>#{event.description}</div>" if event.description
      h += "<div class='members'>#{event.members}</div>" if event.members
      h += "<div class='menu'>"
      h += "<a href='#{url}/#{event.id}/edit'><%= I18n.t('views.links.edit') %></a>" if event.editable
      h += "<a href='#{url}/#{event.id}/delete'><%= I18n.t('views.links.delete') %></a>" if event.deletable
      h += "</div>"
      h += "</div>"
      $(this).append(h)

      name = ".fc-popup"
      view = $(this).find(name)
      hgt = view.outerHeight()

      if $(this).offset().top - hgt < 0
        view.addClass("position-bottom")
        view.css("bottom", (hgt * (-1) - 15) + "px")
        $("#{name} style").replaceWith("<style>#{name}:after { bottom: " + (hgt - 5) + "px; }</style>")
      else
        view.addClass("position-top")
        view.css("bottom", "20px")
        $("#{name} style").replaceWith("<style>#{name}:after { bottom: -13px; }</style>")

    eventDrop: (event, delta, revertFunc) ->
      end = null
      end = event.end.format() if event.end
      $.ajax
        type: 'PUT'
        url: "#{url}/" + event.id + ".json"
        data: { item: { api: 'drop', api_start: event.start.format(), api_end: end } }
        error: (xhr, status, error) ->
          revertFunc()
    eventResize: (event, delta, revertFunc) ->
      $.ajax
        type: 'PUT'
        url: "#{url}/" + event.id + ".json"
        data: { item: { api: 'resize', api_start: event.start.format(), api_end: event.end.format() } }
        error: (xhr, status, error) ->
          revertFunc()

  @renderController: (selector) ->
    controller = $(selector)
    $(selector).fullCalendar({
      lang: 'ja'
      titleFormat: {
        month: "<%= I18n.t('gws/schedule.calendar.titleFormat.month') %>",
        week: "<%= I18n.t('gws/schedule.calendar.titleFormat.week') %>",
        day: "<%= I18n.t('gws/schedule.calendar.titleFormat.day') %>"
      },
      timeFormat: 'HH:mm'
      columnFormat: { month: 'ddd', week: 'M/D [(]ddd[)]', day: 'M/D [(]ddd[)]' }
      defaultView: 'basicWeek',
      header: { left: 'today prev next title', right: '' },
      contentHeight: 'auto',
      eventSources: []
    })
    controller.find('.fc-today-button').click ->
      $('.calendar.multiple .fc-today-button').click()
    controller.find('.fc-prev-button').click ->
      $('.calendar.multiple .fc-prev-button').click()
    controller.find('.fc-next-button').click ->
      $('.calendar.multiple .fc-next-button').click()
